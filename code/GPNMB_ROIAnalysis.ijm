/* ImageJ macro using .tif stacks containing 4 channels from MacNets plates (i.e. stained cell images),
 * uses ROIs generated by GPNMB_Segmentation macro and gets intensity averages, other measurements from specified channel.
 * Tested with ImageJ/Fiji version 2.16.0/1.54p.; WARNING: setBatchMode(true) seems to break this macro...
 * Written by Lisa K. Polzer (0009-0007-5279-0883) for the GPNMB project. 
 */

//set up for macro: define paths, path variables, ImageJ settings
parentDir = getDirectory("Choose parent directory."); //this dir should contain the raw images
inputDirectory = getDirectory("Choose input directory."); //this dir should contain the segmented and filtered images
getDateAndTime(year, month, dayOfWeek, dayOfMonth, hour, minute, second, msec); //‘month’ and ‘dayOfWeek’ are zero-based indices
dateAndTime = "Date_"+year+"-"+month+"-"+dayOfMonth+"_Time_" +hour+"-"+minute+"-"+second;
fileList = getFileList(inputDirectory);
chosen_channel = getString("Choose channel for measurements", 1);
run("Input/Output...", "jpeg=85 gif=-1 file=.csv use_file copy_row save_column save_row"); //configure I/O settings: TIFFs are saved with MSB (big endian) by default
outputFile = inputDirectory + chosen_channel+"_allResults"+dateAndTime+".csv";

//set up of macro's main function
setBatchMode(false); //batchMode(true) lets the script run without displaying images in the GUI
for (i = 0; i < fileList.length; i++){
	if (matches(fileList[i], ".*filtered.*")) { //limit the function to filtered images
		main (inputDirectory, parentDir);
	}
}
setBatchMode(false);

function main (inputDirectory, parentDir) {
	//open Iba1/MAP2 filtered masks, open chosen channel raw images
	open(inputDirectory+fileList[i]);
	filename = getTitle();
	// find first file extension (first dot)
	dot = indexOf(filename, ".");
	if (dot > 0) {
	    // truncate at extension and remove one additional preceding character
	    filename_base = substring(filename, 0, dot - 1);
	} 
	filename_chosen = filename_base+chosen_channel+".TIF"; //reconstitute raw image filename
	print("Analysing "+filename_chosen+"...");
	open(parentDir+filename_chosen);
	
	//get ROIs from cell masks
	selectImage(filename);
	setAutoThreshold("Default no-reset");
	run("Analyze Particles...", "size=0-Infinity pixel circularity=0-1 show=Nothing clear include add"); //get Iba1/MAP2-based ROIs via thresholding mask, note: edges included 
	
	//select chosen channel, add ROIs to ROI manager
	selectImage(filename_chosen);
	run("Set Measurements...", "area mean min max shape redirect=None decimal=3");
	roiManager("measure");
	
	//save results table in shared and individual files
	File.append(filename_base, outputFile);
	ROI_number = Table.size();
	for (r = 0; r < ROI_number; r++) { //define measurements to be saved from ROIs
	    roiIndex = r + 1;  //ROI index (1-based)
	    ROI_mean = Table.get("Mean", r);  //get mean intensity
	    ROI_min  = Table.get("Min", r);  //get min intensity
	    ROI_max  = Table.get("Max", r);  //get max intensity
	    ROI_area = Table.get("Area", r);  //get area
	    ROI_circ = Table.get("Circ.", r);  //get circularity
	    File.append(roiIndex + "," + ROI_mean + "," + ROI_min + "," + ROI_max + "," + ROI_area + "," + ROI_circ, outputFile);
	}
	results_path = inputDirectory + File.separator + filename_base + chosen_channel + "_Results"+dateAndTime+".csv";
	Table.save(results_path);
	
	// close all images
	close("*");
}

// close Results table (if open)
if (isOpen("Results")) {
	selectWindow("Results");
	run("Close");
}
	
// reset and close ROI Manager
roiManager("reset");
run("ROI Manager...");
selectWindow("ROI Manager");
run("Close");

print("\nFinished running macro 'GPNMB_ROIAnalysis'!\n");
